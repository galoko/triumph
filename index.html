<html>
	<head>
		<style>
			body {
				margin: 0
			}
		</style>
	</head>
	<body>
		<canvas id="scene"></canvas>
	</body>
	<script id="paint.vert" type="x-shader/x-vertex">
	attribute vec3 vertexPosition;

	void main()
	{
		gl_Position = vec4(vertexPosition, 1);
	}
	</script>
	<script id="paint.frag" type="x-shader/x-fragment">
	#define POINTS_MAP_RESOLUTION 32.0
	precision mediump float;
	precision lowp int;
	precision lowp sampler2D;

	uniform sampler2D points;

	vec2 getNext(vec2 v) {
		vec2 result;
		float nextX = v.x + 1.0;

		result.y = v.y + floor(nextX / POINTS_MAP_RESOLUTION);
		result.x = mod(nextX, POINTS_MAP_RESOLUTION);

		return result;
	}

	bool isTrail(vec4 pixel) {
		int r = int(pixel.r * 255.0);
		int g = int(pixel.g * 255.0);
		int b = int(pixel.b * 255.0);
		int a = int(pixel.a * 255.0);
		return (r == 255 && g == 255 && b == 255 && a == 255);
	}

	vec2 pixelToScreenCoord(vec4 pixel) {
		return vec2(
				pixel.r * 255.0 + pixel.g * (255.0 * 256.0),
				pixel.b * 255.0 + pixel.a * (255.0 * 256.0)
			);
	}

	void main()
	{
		vec2 point = vec2(gl_FragCoord.x, gl_FragCoord.y);

		float intensity = 0.0;

		vec2 coord = vec2(0.5);
		for (int i = 0; i < 400; i++) {
			vec2 nextCoord = getNext(coord);
			
			vec4 pixel1 = texture2D(points, coord / POINTS_MAP_RESOLUTION);
			vec4 pixel2 = texture2D(points, nextCoord / POINTS_MAP_RESOLUTION);

			bool trail1 = isTrail(pixel1);
			bool trail2 = isTrail(pixel2);
			
			if (trail1 && trail2) {
				break;
			}
			if (!trail1 && !trail2) {	
				// line
				vec2 line1 = pixelToScreenCoord(pixel1);
				vec2 line2 = pixelToScreenCoord(pixel2);

				vec2 delta = line2 - line1;
				
				float lineLength = length(delta);
				vec2 normal = delta / lineLength;
				vec2 tangent = vec2(-normal.y, normal.x);

				float tangentDistance = abs(dot(line1 - point, tangent));

				float normalDistance1 = dot(line1 - point, normal);
				float normalDistance2 = -dot(line2 - point, normal);
				float normalDistance = max(max(normalDistance1, normalDistance2), 0.0);

				intensity = max(intensity, 1.0 / sqrt(tangentDistance * tangentDistance + normalDistance * normalDistance));
			}

			coord = nextCoord;
		}

		gl_FragColor = vec4(vec3(min(intensity, 1.0)), 1.0);
	}
	</script>
	<script src="index.js"></script>
</html>